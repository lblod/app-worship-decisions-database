PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH ?g {
    ?secondOrgaanInTime org:hasPost ?secondLidVanRechtswege .
    ?secondLidVanRechtswege ?psecondLidVanRechtswege ?osecondLidVanRechtswege .
  }
} INSERT {
  GRAPH ?g {
    ?secondOrgaanInTime org:hasPost ?firstLidVanRechtswege .
  }
} WHERE {
  GRAPH ?g {
    ?worshipService a ere:BestuurVanDeEredienst .
    ?orgaan besluit:bestuurt ?worshipService .

    ?firstOrgaanInTime mandaat:isTijdspecialisatieVan ?orgaan ;
      mandaat:bindingStart "2023-04-01T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> ;
      mandaat:bindingEinde "2026-04-01T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> ;
      org:hasPost ?firstLidVanRechtswege .

    ?firstLidVanRechtswege org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5972fccd87f864c4ec06bfbd20b5008b> .

    ?secondOrgaanInTime mandaat:isTijdspecialisatieVan ?orgaan ;
      mandaat:bindingStart "2026-04-01T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> ;
      mandaat:bindingEinde "2029-04-01T00:00:00"^^<http://www.w3.org/2001/XMLSchema#dateTime> ;
      org:hasPost ?secondLidVanRechtswege .

    ?secondLidVanRechtswege org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5972fccd87f864c4ec06bfbd20b5008b> ;
      ?psecondLidVanRechtswege ?osecondLidVanRechtswege .
  }
}