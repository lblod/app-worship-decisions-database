PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuursorgaan skos:prefLabel ?oldBestuursorgaanLabel.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuursorgaan skos:prefLabel ?newBestuursorgaanLabel.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }

    ?bestuurseenheid a ?type;
      skos:prefLabel ?bestuurseenheidLabel.

    ?bestuursorgaan besluit:bestuurt ?bestuurseenheid ;
      skos:prefLabel ?oldBestuursorgaanLabel ;
      besluit:classificatie ?classificationBestuursorgaan .

    ?classificationBestuursorgaan skos:prefLabel ?classificationBestuursorgaanLabel.

    BIND(CONCAT(?classificationBestuursorgaanLabel, " ", ?bestuurseenheidLabel) as ?newBestuursorgaanLabel)
  }
}
