PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?persoon a foaf:Person;
           mu:uuid ?uuidPersoon;
           foaf:firstName ?classificatie;
           foaf:familyName ?naam;
           foaf:member ?bestuurseenheid;
           foaf:account ?account.
  ?account a foaf:OnlineAccount;
           mu:uuid ?uuidAccount;
           foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
           ext:sessionRole "LoketLB-toezichtGebruiker", "LoketLB-bbcdrGebruiker". 
           }
  GRAPH ?g {
  ?persoon a foaf:Person;
           mu:uuid ?uuidPersoon;
           foaf:firstName ?classificatie;
           foaf:familyName ?naam;
           foaf:member ?bestuurseenheid;
           foaf:account ?account.
  ?account a foaf:OnlineAccount;
           mu:uuid ?uuidAccount;
           foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
           ext:sessionRole "LoketLB-toezichtGebruiker", "LoketLB-bbcdrGebruiker".
          }
}
WHERE {
     ?bestuurseenheid a besluit:Bestuurseenheid;
     mu:uuid ?uuid;
     skos:prefLabel ?naam;
     besluit:classificatie/skos:prefLabel ?classificatie.

     BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
     BIND(MD5(?volledigeNaam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)
     BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/",?uuid)) AS ?g )

     VALUES ?bestuurseenheid  {
      <http://data.lblod.info/id/representatieveOrganen/e224c637ba8bb0e5dfbb87da225b4652> 
      <http://data.lblod.info/id/representatieveOrganen/859ae3dff21dcfd97e70d180569ebad1>
      <http://data.lblod.info/id/representatieveOrganen/c98e270d84a8455b2f4bf16b915aeff2>
      <http://data.lblod.info/id/representatieveOrganen/78dcd875a7f2a7d0b5767c9fd8052efa>
      <http://data.lblod.info/id/representatieveOrganen/2089dc85d4ba48a7f28ee4b521af2b26>
      <http://data.lblod.info/id/representatieveOrganen/2267e8e132b5556bd4d0b454c9383ca0>
      <http://data.lblod.info/id/representatieveOrganen/45a0c64b0f2ee4ac0fa8c151e6f2209c>
      <http://data.lblod.info/id/representatieveOrganen/5871503aa86b44cd470f97228a2ce413>
      <http://data.lblod.info/id/representatieveOrganen/bf83abb71bd810ad1ebe05ac8cb72a1c>
      <http://data.lblod.info/id/representatieveOrganen/0ebb44c2ef14d86978ea85e74d128ad1>
      <http://data.lblod.info/id/representatieveOrganen/6f79a1b89678b85009484da7c4a104bc>
      <http://data.lblod.info/id/representatieveOrganen/b4d22cde910a7b58b6e4d9c1d3b15fbb>
     }
}
